// получить табличную часть документа 

Перем 
		_фРаботаемВКонфигурации_ЗиК,
		_СпрВидыРасчетов;

//******************************************************************************
//  глПредставлениеВидаРасчета(ПарамВидРасчета)
//
//  Параметры:
//    ПарамВидРасчета - вид расчета
//
//  Возвращаемое значение:
//    строка - представление заданного вида расчета
//
//  Описание:
//    Возвращает описание вида расчета, заданное пользователем или
//    определенное в конфигураторе
//
Функция ПредставлениеВидаРасчета(ПарамВидРасчета) Экспорт

  Если ПустоеЗначение(ПарамВидРасчета) = 1 Тогда
    Возврат "";
  КонецЕсли;
  Если ТипЗначения(ПарамВидРасчета) <> 14 Тогда
    Возврат "";
  КонецЕсли;

	Если _фРаботаемВКонфигурации_ЗиК = 1 Тогда
		Если _СпрВидыРасчетов.НайтиПоРеквизиту("Расчет",ПарамВидРасчета,1) = 1 Тогда
			Если ПустоеЗначение(_СпрВидыРасчетов.Наименование) = 0 Тогда
				Возврат _СпрВидыРасчетов.Наименование
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Метаданные.ВидРасчета(ПарамВидРасчета.Код).Представление()

КонецФункции // глПредставлениеВидаРасчета 

//******************************************************************************
//	РасчетыДокумента()
//
//	Параметры:
//		нет
//
//	Описание:
//		
//
Процедура РасчетыДокумента(ВыбДокумент, Таб)

    фСоздатьКолонки = 0;
	    
	_фРаботаемВКонфигурации_ЗиК = 0;
	Если Метаданные.Справочник("ВидыРасчетов") <> "Метаданные" Тогда
		_фРаботаемВКонфигурации_ЗиК = 1;
		_СпрВидыРасчетов = СоздатьОбъект("Справочник.ВидыРасчетов");
	КонецЕсли;
	
	ЕстьЗаписиЖурнРасчетов = 0;
	Для Номер=1 По Метаданные.ЖурналРасчетов() Цикл
		МетаданныеЖР = Метаданные.ЖурналРасчетов(Номер);
		ЖР = СоздатьОбъект(МетаданныеЖР.ПолныйИдентификатор());
		
		Если ЖР.ВыбратьЗаписиПоДокументу(ВыбДокумент)=1 Тогда
			
			Если фСоздатьКолонки = 0 Тогда
				фСоздатьКолонки = 1;
				Таб.НоваяКолонка("ПериодРегистрации", "Строка");
				Таб.НоваяКолонка("Объект", "Справочник");
				Таб.НоваяКолонка("ВидРасчета", "Строка");
				Таб.НоваяКолонка("Результат", "Число");
				Таб.НоваяКолонка("Начало", "Дата");
				Таб.НоваяКолонка("Окончание", "Дата");
				Таб.НоваяКолонка("Дни", "Число");
				Таб.НоваяКолонка("Часы", "Число");
				Таб.НоваяКолонка("Рассчитана", "Число");
				Таб.НоваяКолонка("Исправлена", "Число");
				Таб.НоваяКолонка("Фиксирована", "Число");
				Таб.НоваяКолонка("Сторно", "Число");
				Таб.НоваяКолонка("Перерасчет", "Число");
				
				Таб.НоваяКолонка("Документ", "Документ");
				Таб.НоваяКолонка("РодительскийДокумент", "Документ");
			КонецЕсли;
			
			ЕстьЗаписиЖурнРасчетов = 1;
			ПериодРегистрации = "";
			ОбъектРасчета = "";
            
			
			Таб.НоваяСтрока();
			Таб.ПериодРегистрации = МетаданныеЖР.Представление();

			Пока ЖР.ПолучитьЗапись()=1 Цикл      
				
				Если ОбъектРасчета<>ЖР.Объект Тогда
					ОбъектРасчета = ЖР.Объект;
				КонецЕсли;
				Если ПериодРегистрации<>ЖР.ПериодРегистрации.ОписательПериода Тогда
					ПериодРегистрации = ЖР.ПериодРегистрации.ОписательПериода;
				КонецЕсли;
				
				Таб.НоваяСтрока();
				Таб.ПериодРегистрации = ПериодРегистрации;
				Таб.Объект = ОбъектРасчета;
				Таб.ВидРасчета = ПредставлениеВидаРасчета(ЖР.ВидРасч);
				Таб.Результат = ЖР.Результат;
				Таб.Начало = ЖР.ДатаНачала;
				Таб.Окончание = ЖР.ДатаОкончания;

				Таб.Рассчитана = ЖР.Рассчитана;
				Таб.Исправлена = ЖР.Исправлена;
				Таб.Фиксирована = ЖР.Фиксирована;
				Таб.Сторно = ЖР.Сторно;
				Таб.Перерасчет = ЖР.Перерасчет;
				Таб.Документ = ЖР.Документ;
				Таб.РодительскийДокумент = ЖР.РодительскийДокумент;

				Если МетаданныеЖР.Идентификатор="Зарплата" Тогда
					Таб.Дни = ЖР.Дни;
					Таб.Часы = ЖР.Часы;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	Если ЕстьЗаписиЖурнРасчетов=0 Тогда
		Стр = "Нет записей по документу !!";
		Таб.НоваяКолонка("Колонка", "Строка");
		Таб.НоваяСтрока();
		Таб.Колонка = Стр;
	КонецЕсли;
	
КонецПроцедуры	// РасчетыДокумента

Функция Выполнить(Тест) Экспорт

	Результат=СоздатьОбъект("ТаблицаЗначений");
         
	ВыбДокумент = Тест.ОбъектТестирования();
	Если ВыбДокумент.Выбран()=0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ВыбДокумент.Проведен()=0 Тогда
		Стр = "Документ не проведен !!";
		Результат.НоваяКолонка("Колонка", "Строка");
		Результат.НоваяСтрока();
		Результат.Колонка = Стр;
		Возврат Результат;
	КонецЕсли;
	
	Если (Метаданные.Документ(ВыбДокумент.Вид()).Расчет=0) Тогда
		Стр = "Выберите документ, формирующий записи журналов расчетов!";
		Результат.НоваяКолонка("Колонка", "Строка");
		Результат.НоваяСтрока();
		Результат.Колонка = Стр;
		Возврат Результат;
	КонецЕсли;
	     
	РасчетыДокумента(ВыбДокумент, Результат);
	
	Возврат Результат;

КонецФункции