Перем ТекущийКаталог;
Перем ИмяСпискаТестов;
Перем КоличествоКаталогов;

Функция Сам(Конт)
	Возврат Конт;
КонецФункции

Процедура Конструктор()
	ИмяСпискаТестов="\testlst.txt";
	КоличествоКаталогов=0;
КонецПроцедуры

Функция ПолучитьКоличествоКаталогов() Экспорт
	Возврат КоличествоКаталогов;
КонецФункции

Функция ЭтоКаталог(ИмяФайла)
	Атрибуты="";
	ФС.АтрибутыФайла(ИмяФайла,,Атрибуты);
	Возврат Число(Сред(Атрибуты, 4, 1));
КонецФункции

Функция НайтиСправа(стр, Символ)
	поз=СтрДлина(стр);
	Пока поз>0 Цикл
		Если Сред(стр, поз, 1)=Символ Тогда Прервать; КонецЕсли;
		поз=поз-1;
	КонецЦикла;
	Возврат поз;
КонецФункции

Процедура СохранитьСписокТестов(СписокТестов) Экспорт
	Пром=СоздатьОбъект("ExTableValue");
	Пром.ИзТаблицыЗначений(СписокТестов);
	
	Попытка
		Пром.УдалитьКолонку("Цвет");
		Пром.УдалитьКолонку("Номер");
		Пром.УдалитьКолонку("Пиктограмма");
	Исключение
	КонецПопытки;

	МаксСтрок=Пром.КоличествоСтрок();
	Для С=1 По МаксСтрок Цикл
		НомерСтр=1+МаксСтрок-С;
		Если ЭтоКаталог(Пром.ПолучитьЗначение(НомерСтр,"ИмяФайлаРезультатов"))=1 Тогда
			Пром.УдалитьСтроку(НомерСтр);
		КонецЕсли;
	КонецЦикла;
	
	Если Пром.КоличествоСтрок()=0 Тогда
		ФС.УдалитьФайл(ТекущийКаталог+ИмяСпискаТестов);
	Иначе
		Т=Пром.ВТекст();
		Т.Записать(ТекущийКаталог+ИмяСпискаТестов);
	КонецЕсли
КонецПроцедуры

Функция ДополнитьНулями(Числ)
	Пром=Строка(Числ);
	Пока СтрДлина(Пром)<3 Цикл
		Пром="0"+Пром;
	КонецЦикла;
	Возврат Пром;
КонецФункции

Функция ПолучитьИмяНовыхОжиданий()
	
	МаксНомер=0;
	ТекИмя=ФС.НайтиПервыйФайл(ТекущийКаталог+"\test_???.txt");
	Пока ТекИмя<>"" Цикл
		ТекНомер=Число(Сред(ТекИмя,6,3));
		МаксНомер=Макс(МаксНомер,ТекНомер);
		
		ТекИмя=ФС.НайтиСледующийФайл();
	КонецЦикла;
	
	Возврат "test_"+ДополнитьНулями(МаксНомер+1)+".txt";
КонецФункции

Функция СоздатьОжиданияНовомуТесту()
	Ожидания=СоздатьОбъект("ExTableValue");
	
	//можно ли удалить?
	Ожидания.НоваяКолонка("Затычка","Строка");
	Ожидания.НоваяСтрока();
	Ожидания.Затычка="Затычка";
	
	Т=Ожидания.ВТекст();
	ИмяНовыхОжиданий=ПолучитьИмяНовыхОжиданий();
	Т.Записать(ТекущийКаталог+"\"+ИмяНовыхОжиданий);
	
	Возврат ИмяНовыхОжиданий;
КонецФункции

Функция ПолучитьСписокКаталогов() Экспорт
	Результат=СоздатьОбъект("СписокЗначений");
	
	ИмяФайла=ФС.НайтиПервыйФайл(ТекущийКаталог+"\*.*");
	Пока ИмяФайла<>"" Цикл
		Если (ИмяФайла = ".") Или (ИмяФайла = "CVS") Или (ИмяФайла = "..") Тогда
		ИначеЕсли ЭтоКаталог(ТекущийКаталог+"\"+ИмяФайла)=1 Тогда
			Результат.ДобавитьЗначение(ИмяФайла,ТекущийКаталог+"\"+ИмяФайла);
		КонецЕсли;
		
		ИмяФайла=ФС.НайтиСледующийФайл();
	КонецЦикла;
	
	КоличествоКаталогов=Результат.РазмерСписка();
	Возврат Результат;
КонецФункции

Процедура УстановитьТекущийКаталог(ИмяКаталога) Экспорт
	ТекущийКаталог=СокрЛП(ИмяКаталога);
	ПолучитьСписокКаталогов();
КонецПроцедуры

Функция ПолучитьТекущийКаталог() Экспорт
	Возврат ТекущийКаталог;
КонецФункции

Функция ПолучитьРодительскийКаталог() Экспорт
	ПозицияСлэша=НайтиСправа(ТекущийКаталог, "\");
	Возврат Лев(ТекущийКаталог, ПозицияСлэша-1);
КонецФункции

Функция ПолучитьИменаТестов() Экспорт
	Результат=СоздатьОбъект("СписокЗначений");
	
	Если ФС.СуществуетФайл(ТекущийКаталог+ИмяСпискаТестов)=1 Тогда
		Т=СоздатьОбъект("Текст");
		Пром=СоздатьОбъект("ExTableValue");
		
		Т.Открыть(ТекущийКаталог+ИмяСпискаТестов);
		Пром.ИзТекста(Т);
		
		Результат=Пром.КолонкуВСписок(1);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ПолучитьСписокТестов(СписокТестов) Экспорт
	СписокТестов.Очистить();
	СписокТестов.НоваяКолонка("НазваниеТеста","Строка");
	СписокТестов.НоваяКолонка("ИмяФайлаРезультатов","Строка");
	
	ПодКаталоги=ПолучитьСписокКаталогов();
	Для С=1 По ПодКаталоги.РазмерСписка() Цикл
		Файл="";
		Имя=ПодКаталоги.ПолучитьЗначение(С,Файл);
		СписокТестов.НоваяСтрока();
		СписокТестов.НазваниеТеста=Имя;
		СписокТестов.ИмяФайлаРезультатов=Файл;
	КонецЦикла;

	Если ФС.СуществуетФайл(ТекущийКаталог+ИмяСпискаТестов)=1 Тогда
		Т=СоздатьОбъект("Текст");
		Пром=СоздатьОбъект("ExTableValue");
		
		Т.Открыть(ТекущийКаталог+ИмяСпискаТестов);
		Пром.ИзТекста(Т);
		
		Пром.ВТаблицуЗначений(СписокТестов,1);
	КонецЕсли;
КонецПроцедуры


Процедура НовыйТест(СписокТестов, НазваниеТеста) Экспорт
	СписокТестов.НоваяСтрока();
	СписокТестов.ТекущаяСтрока(СписокТестов.НомерСтроки);// Артур - чтобы сразу переходил на новый тест и записывались данные этого теста
	
	СписокТестов.НазваниеТеста=НазваниеТеста;
	СписокТестов.ИмяФайлаРезультатов=СоздатьОжиданияНовомуТесту();
	
	СохранитьСписокТестов(СписокТестов);
КонецПроцедуры

Процедура УдалитьТест(НомерТеста) Экспорт
	СписокТестов=СоздатьОбъект("ТаблицаЗначений");
	ПолучитьСписокТестов(СписокТестов);
	ИмяФайлаРезультатов=СписокТестов.ПолучитьЗначение(НомерТеста,"ИмяФайлаРезультатов");
	СписокТестов.УдалитьСтроку(НомерТеста);
	
	//В транзакцию???
	ФС.УдалитьФайл(ТекущийКаталог+"\"+ИмяФайлаРезультатов);
	СохранитьСписокТестов(СписокТестов);
	//
КонецПроцедуры

Процедура ПоказатьОжиданияТеста(НомерТеста,ВходныеУсловия,ТаблицаОжиданий) Экспорт
	Т=СоздатьОбъект("Текст");
	Т.Открыть(ТекущийКаталог+"\test_"+ДополнитьНулями(НомерТеста)+".txt");
	
	ПерваяСтрока=СоздатьОбъект("ExValueList");
	ПерваяСтрока.ВсеИзСтрокиСРазделителями(Т.ПолучитьСтроку(1));
	ПерваяСтрока.ВТаблицуЗначений(ВходныеУсловия, 0);
	
	Т.УдалитьСтроку(1);
	
	Пром=СоздатьОбъект("ExTableValue");
	Пром.ИзТекста(Т);
	Пром.ВТаблицуЗначений(ТаблицаОжиданий);
КонецПроцедуры

Процедура СохранитьОжиданияТеста(ИмяФайлаОжиданий,ВходныеУсловия,ОжидаемыйРезультат) Экспорт
	ФайлТекущихОжиданий=ТекущийКаталог+"\"+ИмяФайлаОжиданий;
	
	Пром=СоздатьОбъект("ExTableValue");
	Пром.ИзТаблицыЗначений(ОжидаемыйРезультат);
	Т=Пром.ВТекст();
	
	Если ТипЗначенияСтр(ВходныеУсловия)="ExValueList" Тогда
		Строк=ВходныеУсловия.ВсеВСтрокуСРазделителями();
	Иначе
		ПерваяСтрока=СоздатьОбъект("ExValueList");
		ПерваяСтрока.ИзТаблицыЗначений(ВходныеУсловия);
		Строк=ПерваяСтрока.ВсеВСтрокуСРазделителями();
	КонецЕсли;
	Т.ВставитьСтроку(1,Строк);

	Т.Записать(ФайлТекущихОжиданий);
КонецПроцедуры

Процедура СохранитьУсловияТеста(СписокТестов,ВходныеУсловия,ОжидаемыйРезультат) Экспорт
	СохранитьОжиданияТеста(СписокТестов.ИмяФайлаРезультатов,ВходныеУсловия,ОжидаемыйРезультат);
КонецПроцедуры

Функция КоличествоТестов() Экспорт
	Если ФС.СуществуетФайл(ТекущийКаталог+ИмяСпискаТестов)=0 Тогда
		Возврат 0;
	КонецЕсли;
	Т=СоздатьОбъект("Текст");
	Т.Открыть(ТекущийКаталог+ИмяСпискаТестов);
	//верхняя строчка - описание колонок таблицы и тестом не является.
	Возврат Т.КоличествоСтрок()-1;
КонецФункции

Функция ПолучитьИмяТеста(НомерТеста) Экспорт
	СписокТестов=СоздатьОбъект("ТаблицаЗначений");
	ПолучитьСписокТестов(СписокТестов);
	
	Возврат СписокТестов.ПолучитьЗначение(НомерТеста+ПолучитьКоличествоКаталогов(),"НазваниеТеста");
КонецФункции

Функция ПолучитьИмяФайлаТеста(НомерТеста) Экспорт
	СписокТестов=СоздатьОбъект("ТаблицаЗначений");
	ПолучитьСписокТестов(СписокТестов);
	
	Возврат СписокТестов.ПолучитьЗначение(НомерТеста+ПолучитьКоличествоКаталогов(),"ИмяФайлаРезультатов");
КонецФункции

Процедура СкопироватьТест(СписокТестов) Экспорт
	НовыйТест(СписокТестов,СписокТестов.НазваниеТеста);
КонецПроцедуры

Функция НомерКаталогаПоИмени(Имя) Экспорт
	Каталоги = ПолучитьСписокКаталогов();
	Значение = Каталоги.Получить(СокрЛП(Имя));
	Позиция = Каталоги.НайтиЗначение(Значение);
	Возврат ?(Позиция = 0, 1, Позиция);
КонецФункции

//#if DEBUG
Процедура ПриОткрытии()
	Форма.Параметр._ПриОткрытии();
КонецПроцедуры
//#endif