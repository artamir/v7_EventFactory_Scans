// получить записи журнала расчета для определенного объекта за определенный период регистрации

Перем 
		_фРаботаемВКонфигурации_ЗиК,
		_СпрВидыРасчетов;

//******************************************************************************
//  глПредставлениеВидаРасчета(ПарамВидРасчета)
//
//  Параметры:
//    ПарамВидРасчета - вид расчета
//
//  Возвращаемое значение:
//    строка - представление заданного вида расчета
//
//  Описание:
//    Возвращает описание вида расчета, заданное пользователем или
//    определенное в конфигураторе
//
Функция ПредставлениеВидаРасчета(ПарамВидРасчета) Экспорт

  Если ПустоеЗначение(ПарамВидРасчета) = 1 Тогда
    Возврат "";
  КонецЕсли;
  Если ТипЗначения(ПарамВидРасчета) <> 14 Тогда
    Возврат "";
  КонецЕсли;

	Если _фРаботаемВКонфигурации_ЗиК = 1 Тогда
		Если _СпрВидыРасчетов.НайтиПоРеквизиту("Расчет",ПарамВидРасчета,1) = 1 Тогда
			Если ПустоеЗначение(_СпрВидыРасчетов.Наименование) = 0 Тогда
				Возврат _СпрВидыРасчетов.Наименование
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Метаданные.ВидРасчета(ПарамВидРасчета.Код).Представление()

КонецФункции // глПредставлениеВидаРасчета 

Процедура ПолучитьЗаписиЖурналаРасчетов(Таб, Объект, ИДЖурналаРасчетов, ДатаПериодаРегистрации)
	МетаданныеЖР = Метаданные.ЖурналРасчетов(ИДЖурналаРасчетов);
	ЖР = СоздатьОбъект(МетаданныеЖР.ПолныйИдентификатор());
 
	_фРаботаемВКонфигурации_ЗиК = 0;
	Если Метаданные.Справочник("ВидыРасчетов") <> "Метаданные" Тогда
		_фРаботаемВКонфигурации_ЗиК = 1;
		_СпрВидыРасчетов = СоздатьОбъект("Справочник.ВидыРасчетов");
	КонецЕсли;

	ПериодРегистрации = "";
	ЕстьЗаписиЖурнРасчетов = 0;
	
	Если ЖР.ВыбратьПериодПоОбъекту(Объект, ДатаПериодаРегистрации) = 1 Тогда
		Таб.НоваяКолонка("ПериодРегистрации", "Строка");
		Таб.НоваяКолонка("Объект", "Справочник");
		Таб.НоваяКолонка("ВидРасчета", "Строка");
		Таб.НоваяКолонка("Результат", "Число");
		Таб.НоваяКолонка("Начало", "Дата");
		Таб.НоваяКолонка("Окончание", "Дата");
		Таб.НоваяКолонка("Дни", "Число");
		Таб.НоваяКолонка("Часы", "Число");
		Таб.НоваяКолонка("Рассчитана", "Число");
		Таб.НоваяКолонка("Исправлена", "Число");
		Таб.НоваяКолонка("Фиксирована", "Число");
		Таб.НоваяКолонка("Сторно", "Число");
		Таб.НоваяКолонка("Перерасчет", "Число");
		
		Таб.НоваяКолонка("Документ", "Документ");
		Таб.НоваяКолонка("РодительскийДокумент", "Документ");

		Пока ЖР.ПолучитьЗапись()=1 Цикл      
			ЕстьЗаписиЖурнРасчетов = 1;
			
			Если ПериодРегистрации<>ЖР.ПериодРегистрации.ОписательПериода Тогда
				ПериодРегистрации = ЖР.ПериодРегистрации.ОписательПериода;
			КонецЕсли;
			
			Таб.НоваяСтрока();
			Таб.ПериодРегистрации = ПериодРегистрации;
			Таб.Объект = ЖР.Объект;
			Таб.ВидРасчета = ПредставлениеВидаРасчета(ЖР.ВидРасч);
			Таб.Результат = ЖР.Результат;
			Таб.Начало = ЖР.ДатаНачала;
			Таб.Окончание = ЖР.ДатаОкончания;
	
			Таб.Рассчитана = ЖР.Рассчитана;
			Таб.Исправлена = ЖР.Исправлена;
			Таб.Фиксирована = ЖР.Фиксирована;
			Таб.Сторно = ЖР.Сторно;
			Таб.Перерасчет = ЖР.Перерасчет;
			Таб.Документ = ЖР.Документ;
			Таб.РодительскийДокумент = ЖР.РодительскийДокумент;
	
			Если ИДЖурналаРасчетов = "Зарплата" Тогда
				Таб.Дни = ЖР.Дни;
				Таб.Часы = ЖР.Часы;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьЗаписиЖурнРасчетов=0 Тогда
		Стр = "Нет записей !!";
		Таб.НоваяКолонка("Колонка", "Строка");
		Таб.НоваяСтрока();
		Таб.Колонка = Стр;
	КонецЕсли;
КонецПроцедуры	// ПолучитьЗаписиЖурналаРасчетов

Функция Выполнить(Тест) Экспорт
    // Тест // : TheTest
	Результат=СоздатьОбъект("ТаблицаЗначений");
         
	ВыбЭлемент = Тест.ОбъектТестирования();
	ИдЖурналаРасчетов = Строка(Тест.ПолучитьУсловие("ЖурналРасчетов"));
	ДатаПериодаРегистрации = Тест.ПолучитьУсловие("ДатаПериодаРегистрации");

	Если ВыбЭлемент.Выбран()=0 Тогда
		Стр = "Не выбран элемент !!";
		Результат.НоваяКолонка("Колонка", "Строка");
		Результат.НоваяСтрока();
		Результат.Колонка = Стр;
		Возврат Результат;
	КонецЕсли;

	Если ПустоеЗначение(ДатаПериодаРегистрации) > 0 Тогда
		Стр = "Не выбрана дата периода регистрации !!";
		Результат.НоваяКолонка("Колонка", "Строка");
		Результат.НоваяСтрока();
		Результат.Колонка = Стр;
		Возврат Результат;
	КонецЕсли;
	
	Если ИДЖурналаРасчетов = "" Тогда
		ИДЖурналаРасчетов = "Зарплата"
	КонецЕсли;

	Если Метаданные.ЖурналРасчетов(ИДЖурналаРасчетов) = "Метаданные" Тогда
		Стр = "Не выбран или неверно указан журнал расчетов !!";
		Результат.НоваяКолонка("Колонка", "Строка");
		Результат.НоваяСтрока();
		Результат.Колонка = Стр;
		Возврат Результат;
	КонецЕсли;

	ПолучитьЗаписиЖурналаРасчетов(Результат, ВыбЭлемент, ИДЖурналаРасчетов, ДатаПериодаРегистрации);
	
	Возврат Результат;

КонецФункции