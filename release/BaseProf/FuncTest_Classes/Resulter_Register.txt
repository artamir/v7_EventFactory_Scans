
Функция Выполнить(Тест) Экспорт
	
	Результат=СоздатьОбъект("ТаблицаЗначений");
	
	ПозТочки=Найти(Тест.ЧтоТестируем(),".");
	ИдРегистра=СокрЛП(Сред(Тест.ЧтоТестируем(),ПозТочки+1));
	
	Результат.НоваяКолонка("Знак");
	МетаРег=Метаданные.Регистр(ИдРегистра);
	Для С=1 По МетаРег.Измерение() Цикл
		Результат.НоваяКолонка(МетаРег.Измерение(С).Идентификатор);
	КонецЦикла;
	Для С=1 По МетаРег.Ресурс() Цикл
		Результат.НоваяКолонка(МетаРег.Ресурс(С).Идентификатор);
	КонецЦикла;
	Для С=1 По МетаРег.Реквизит() Цикл
		Результат.НоваяКолонка(МетаРег.Реквизит(С).Идентификатор);
	КонецЦикла;
	
	Р=СоздатьОбъект(Тест.ЧтоТестируем());
	Р.ВыбратьДвиженияДокумента(Тест.ОбъектТестирования());
	Пока Р.ПолучитьДвижение()=1 Цикл
		Результат.НоваяСтрока();
		
		Если Р.Приход=1 Тогда
			Результат.Знак="+";
		ИначеЕсли Р.Расход=1 Тогда
			Результат.Знак="-";
		КонецЕсли;
		
		Для С=2 По Результат.КоличествоКолонок() Цикл
			ИдКолонки=Результат.ПолучитьПараметрыКолонки(С);
			Значение=Р.ПолучитьАтрибут(ИдКолонки);
			Если ТипЗначенияСтр(Значение)="Строка" Тогда
				Значение=СокрЛП(Значение);
			КонецЕсли;
			Результат.УстановитьЗначение(Результат.КоличествоСтрок(),С,Значение);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции
