Функция Сам(Конт)
	Возврат Конт;
КонецФункции

Функция ВСтроку() Экспорт
	Пром=СоздатьОбъект("СписокЗначений");
	Результат="";
	
	//В первой строке укажем названия колонок.
	Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
		ИмяКолонки=Сам(Контекст).ПолучитьПараметрыКолонки(К);
		Результат=Результат+ИмяКолонки+",";
	КонецЦикла;
	Результат=Результат+РазделительСтрок;
	
	//В остальные строки выгрузим саму таблицу
	Для С=1 По Сам(Контекст).КоличествоСтрок() Цикл
		Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
			Результат=Результат+Сам(Контекст).ПолучитьЗначение(С,К)+",";
		КонецЦикла;
		Результат=Результат+РазделительСтрок;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция ВТекст() Экспорт
	СтрокаТаблицы=СоздатьОбъект("ExValueList_old");
	Результат=СоздатьОбъект("Текст");
	
	//В первой строке укажем названия и тип колонок. В значение спрячем название, в представление - тип.
	Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
		ТипКолонки="";
		ИмяКолонки=Сам(Контекст).ПолучитьПараметрыКолонки(К,ТипКолонки);
		СтрокаТаблицы.ДобавитьЗначение(ИмяКолонки,ТипКолонки);
	КонецЦикла;
	Результат.ДобавитьСтроку(СтрокаТаблицы.ВсеВСтрокуСРазделителями());
	СтрокаТаблицы.УдалитьВсе();
	
	//В остальные строки выгрузим саму таблицу
	Для С=1 По Сам(Контекст).КоличествоСтрок() Цикл
		Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
			Представление=Сам(Контекст).ПолучитьПараметрыКолонки(К);
			Значение=Сам(Контекст).ПолучитьЗначение(С,К);

			СтрокаТаблицы.ДобавитьЗначение(Значение,Представление);
		КонецЦикла;
		Результат.ДобавитьСтроку(СтрокаТаблицы.ВсеВСтрокуСРазделителями());
		
		СтрокаТаблицы.УдалитьВсе();
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ИзТекста(Текст) Экспорт
	Сам(Контекст).Очистить();
	СтрокаТаблицы=СоздатьОбъект("ExValueList_old");
	
	Если Текст.КоличествоСтрок()=0 Тогда
		Возврат;
	КонецЕсли;
	//В первой строке - параметры колонок
	СтрокаТаблицы.ВсеИзСтрокиСРазделителями(Текст.ПолучитьСтроку(1));
	Для К=1 По СтрокаТаблицы.РазмерСписка() Цикл
		ТипКолонки="";
		ИмяКолонки=СтрокаТаблицы.ПолучитьЗначение(К,ТипКолонки);
		Сам(Контекст).НоваяКолонка(ИмяКолонки,ТипКолонки);
	КонецЦикла;
	
	Если Текст.КоличествоСтрок()=1 Тогда
		Возврат;
	КонецЕсли;
	//В остальных - значения.
	Для С=2 По Текст.КоличествоСтрок() Цикл
		ПромСтр=Текст.ПолучитьСтроку(С);
		
		//Такого не должно быть, но иногда бывает.
		//Теперь бывает, когда тесту нужно обязательно сломаться.
		//Если СокрЛП(ПромСтр)="" Тогда
		//	Сообщить("Пустая строка");
		//	Продолжить;
		//КонецЕсли;
		
		СтрокаТаблицы.ВсеИзСтрокиСРазделителями(ПромСтр);
		Сам(Контекст).НоваяСтрока();
		Для К=1 По СтрокаТаблицы.РазмерСписка() Цикл
			ИдКолонки="";
			Значение=СтрокаТаблицы.ПолучитьЗначение(К,ИдКолонки);
			Сам(Контекст).УстановитьЗначение(С-1,ИдКолонки,Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИзТаблицыЗначений(Таблица) Экспорт
	Сам(Контекст).Очистить();
	Для К=1 По Таблица.КоличествоКолонок() Цикл
		ИмяТипа="";
		ИдентификаторКолонки=Таблица.ПолучитьПараметрыКолонки(К,ИмяТипа);
		Сам(Контекст).НоваяКолонка(ИдентификаторКолонки,ИмяТипа);
	КонецЦикла;
	
	Для С=1 По Таблица.КоличествоСтрок() Цикл
		Сам(Контекст).НоваяСтрока();
		Для К=1 По Таблица.КоличествоКолонок() Цикл
			Сам(Контекст).УстановитьЗначение(С,К,Таблица.ПолучитьЗначение(С,К));
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ВТаблицуЗначений(Таблица, Мягко) Экспорт
	ИдентификаторыКолонок=СоздатьОбъект("СписокЗначений");

	Если Мягко=0 Тогда
		Таблица.Очистить();
	Иначе
		Для К=1 По Таблица.КоличествоКолонок() Цикл
			ИдентификаторыКолонок.ДобавитьЗначение(Таблица.ПолучитьПараметрыКолонки(К));
		КонецЦикла;
	КонецЕсли;
	
	Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
		ИмяТипа="";
		ИдентификаторКолонки=Сам(Контекст).ПолучитьПараметрыКолонки(К,ИмяТипа);
		Если (Мягко=0) Или
			 (ИдентификаторыКолонок.Принадлежит(ИдентификаторКолонки)=0) Тогда
			Таблица.НоваяКолонка(ИдентификаторКолонки,ИмяТипа);
		КонецЕсли;
	КонецЦикла;
	
	Для С=1 По Сам(Контекст).КоличествоСтрок() Цикл
		Таблица.НоваяСтрока();
		Для К=1 По Сам(Контекст).КоличествоКолонок() Цикл
			ИдентификаторКолонки=Сам(Контекст).ПолучитьПараметрыКолонки(К);
			Таблица.УстановитьЗначение(Таблица.КоличествоСтрок(),ИдентификаторКолонки,Сам(Контекст).ПолучитьЗначение(С,К));
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьСтрокуИзСЗ(Список) Экспорт
	Сам(Контекст).НоваяСтрока();
	Для С=1 По Список.РазмерСписка() Цикл
		Колонка="";
		Значение=Список.ПолучитьЗначение(С,Колонка);
		Сам(Контекст).УстановитьЗначение(Сам(Контекст).КоличествоСтрок(),СокрЛП(Колонка),Значение);
	КонецЦикла;
КонецПроцедуры

Функция КолонкуВСписок(НомерКолонки) Экспорт
	Результат=СоздатьОбъект("СписокЗначений");
	Для С=1 По Сам(Контекст).КоличествоСтрок() Цикл
		Результат.ДобавитьЗначение(Сам(Контекст).ПолучитьЗначение(С,НомерКолонки));
	КонецЦикла;
	Возврат Результат;
КонецФункции

