Функция Сам(Конт)
	Возврат Конт;
КонецФункции

Функция ВсеВСтрокуСРазделителями() Экспорт
	Пром=СоздатьОбъект("СписокЗначений");
	Результат=СоздатьОбъект("СписокЗначений");
	
	Для С=1 По Сам(Контекст).РазмерСписка() Цикл
		Представление="";
		Значение=Сам(Контекст).ПолучитьЗначение(С,Представление);
		Если ТипЗначенияСтр(Значение)="Строка" Тогда
			Значение=СтрЗаменить(СокрЛП(Значение), РазделительСтрок, "\n");
		КонецЕсли;
		
		Пром.ДобавитьЗначение(ЗначениеВСтрокуВНутр(Значение));
		//Сообщить("Доб. значение в строку "+ЗначениеВСтрокуВНутр(Значение));
		Пром.ДобавитьЗначение(СокрЛП(Представление));
		//Сообщить("Доб. предс-ие в строку "+СокрЛП(Представление));
		
		Результат.ДобавитьЗначение(Пром.ВСтрокуСРазделителями());
		Пром.УдалитьВсе();
	КонецЦикла;
	
	Возврат Результат.ВСтрокуСРазделителями();
КонецФункции

Функция ВСтроку() Экспорт
	Возврат ВсеВСтрокуСРазделителями();
КонецФункции

Процедура ВсеИзСтрокиСРазделителями(Строк) Экспорт
	//Сообщить("Достаем из строки: "+Строк);
	Парам=СоздатьОбъект("СписокЗначений");
	Пром=СоздатьОбъект("СписокЗначений");
	                                     
	Сам(Контекст).УдалитьВсе();

	Парам.ИзСтрокиСРазделителями(Строк);
	//Сообщить(Парам.РазмерСписка());
	Для С=1 По Парам.РазмерСписка() Цикл
		Пром.ИзСтрокиСРазделителями(Парам.ПолучитьЗначение(С));
		
		Значение=ЗначениеИзСтрокиВнутр(Пром.ПолучитьЗначение(1));
		
		//Сообщить("Достали из строки значение "+Значение);
		Представление=Пром.ПолучитьЗначение(2);
		//Сообщить("Достали из строки предс-ие "+Представление);
		
		Сам(Контекст).ДобавитьЗначение(Значение,Представление);
	КонецЦикла;
КонецПроцедуры

Процедура ИзСтроки(Строк) Экспорт
	ВсеИзСтрокиСРазделителями(Строк);
КонецПроцедуры

Процедура ИзСпискаЗначений(Список) Экспорт
	Сам(Контекст).УдалитьВсе();
	Для С=1 По Список.РазмерСписка() Цикл
		Представление="";
		Значение=Список.ПолучитьЗначение(С,Представление);
		Сам(Контекст).ДобавитьЗначение(Значение,Представление);
	КонецЦикла;
КонецПроцедуры

Функция ВСписокЗначений() Экспорт
	Результат=СоздатьОбъект("СписокЗначений");
	Для С=1 По Сам(Контекст).РазмерСписка() Цикл
		Представление="";
		Значение=Сам(Контекст).ПолучитьЗначение(С,Представление);
		Результат.ДобавитьЗначение(Значение,Представление);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ИзТаблицыЗначений(Таблица) Экспорт
	Сам(Контекст).УдалитьВсе();
	
	Таблица.ВыбратьСтроки();
	Пока Таблица.ПолучитьСтроку()=1 Цикл
		Сам(Контекст).ДобавитьЗначение(Таблица.Значение,Таблица.Представление);
	КонецЦикла;
КонецПроцедуры

Функция ВТаблицуЗначений(Таблица=0, Мягко=0) Экспорт
	Если Таблица=0 Тогда
		Таблица=СоздатьОбъект("ТаблицаЗначений");
	КонецЕсли;
	Если Мягко=0 Тогда
		Таблица.Очистить();
		Таблица.НоваяКолонка("Значение");
		Таблица.НоваяКолонка("Представление","Строка");
	Иначе
		ЕстьЗначение=0;
		ЕстьПредставление=0;
		Для К=1 По Таблица.КоличествоКолонок() Цикл
			ИдентификаторКолонки=Таблица.ПолучитьПараметрыКолонки(К);
			Если ИдентификаторКолонки="Значение" Тогда
				ЕстьЗначение=1
			ИначеЕсли ИдентификаторКолонки="Представление" Тогда
				ЕстьПредставление=1;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьЗначение=0 Тогда
			Таблица.НоваяКолонка("Значение");
		КонецЕсли;
		Если ЕстьПредставление=0 Тогда
			Таблица.НоваяКолонка("Представление");
		КонецЕсли;
	КонецЕсли;

	Для С=1 По Сам(Контекст).РазмерСписка() Цикл
		Представление="";
		Значение=Сам(Контекст).ПолучитьЗначение(С,Представление);
		Таблица.НоваяСтрока();
		Таблица.Значение=Значение;
		Таблица.Представление=Представление;
	КонецЦикла;
	
	Возврат Таблица;
КонецФункции

Процедура ИзСтрокиТЗ(Таблица,НомСтроки) Экспорт
	Сам(Контекст).УдалитьВсе();
	Для К=1 По Таблица.КоличествоКолонок() Цикл
		Значение=Таблица.ПолучитьЗначение(НомСтроки,К);
		Представление=Таблица.ПолучитьПараметрыКолонки(К);
		Сам(Контекст).ДобавитьЗначение(Значение,Представление);
	КонецЦикла;
КонецПроцедуры

Функция НайтиПоПредставлению(Представление) Экспорт
	Значение=Сам(Контекст).Получить(Представление);
	Возврат Сам(Контекст).НайтиЗначение(Значение);
КонецФункции
