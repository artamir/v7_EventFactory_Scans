Перем Тесты;
Перем ТекущийТест, фНаборВыполнен;
Перем _Результат, _СообщатьОПрогрессе;

Процедура Конструктор()
	Тесты = СоздатьОбъект("СписокЗначений");
КонецПроцедуры

Процедура ЗапуститьТесты() Экспорт
	Пока Тесты.РазмерСписка()>0 Цикл
		ТекущийТест = Тесты.ПолучитьЗначение(1); 
		Тесты.УдалитьЗначение(1);
		Если _СообщатьОПрогрессе = 1 Тогда
			Сообщить(ТекущийТест.Описание());
		КонецЕсли;
		//Возможен запуск теста или вложенного набора тестов
		ТекущийТест.Запустить(_Результат);
		//Если ТекущийТест.Завершен()=0 Тогда Возврат; КонецЕсли;
	КонецЦикла;
	фНаборВыполнен=1;
КонецПроцедуры

Процедура Запустить(Результат, СообщатьОПрогрессе) Экспорт
	_Результат=?(ТипЗначенияСтр(Результат)="РезультатТеста", Результат, СоздатьОбъект("РезультатТеста"));
	_СообщатьОПрогрессе=СообщатьОПрогрессе;
	_Результат.ОтметитьВремяЗапуска();
	фНаборВыполнен=0;
	ЗапуститьТесты();
КонецПроцедуры

Функция Результат() Экспорт
	Возврат _Результат;
КонецФункции

Функция Выполнен() Экспорт
	Если фНаборВыполнен=1 Тогда Возврат 1; КонецЕсли;
	//Если ТекущийТест.Завершен()=0 Тогда Возврат 0; КонецЕсли;
	ЗапуститьТесты();
	Возврат фНаборВыполнен;
КонецФункции

Процедура ДобавитьТест(Тест) Экспорт
	Тесты.ДобавитьЗначение(Тест);
КонецПроцедуры

Процедура Добавить(ИмяКласса, ИмяМетода) Экспорт
	Тест = СоздатьОбъект(ИмяКласса);
	Тест.Инит(ИмяМетода);
	ДобавитьТест(Тест);
КонецПроцедуры

Функция ВсегоТестов() Экспорт
	Возврат Тесты.РазмерСписка();
КонецФункции

