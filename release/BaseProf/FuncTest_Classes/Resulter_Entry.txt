
Функция СтруктураПроводки()
	Результат=СоздатьОбъект("СписокЗначений");
	Результат.ДобавитьЗначение("СчетДТ");
	Для С=1 По Метаданные.МаксимальноеКоличествоСубконто Цикл
		Результат.ДобавитьЗначение("ДтСубконто"+С);
	КонецЦикла;
	Результат.ДобавитьЗначение("СчетКТ");
	Для С=1 По Метаданные.МаксимальноеКоличествоСубконто Цикл
		Результат.ДобавитьЗначение("КтСубконто"+С);
	КонецЦикла;
	Результат.ДобавитьЗначение("Сумма");
	Результат.ДобавитьЗначение("Количество");
	Результат.ДобавитьЗначение("ВалСумма");
	Результат.ДобавитьЗначение("Валюта");

	Для С=1 По Метаданные.РеквизитПроводки() Цикл
		Результат.ДобавитьЗначение(Метаданные.РеквизитПроводки(С).Идентификатор);
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция Выполнить(Тест) Экспорт
	
	Результат=СоздатьОбъект("ТаблицаЗначений");
	
	СП=СтруктураПроводки();
	Для С=1 По СП.РазмерСписка() Цикл
		Результат.НоваяКолонка(СП.ПолучитьЗначение(С));
	КонецЦикла;
	
	О=СоздатьОбъект("Операция");
	Если О.НайтиОперацию(Тест.ОбъектТестирования())=1 Тогда
		О.ВыбратьПроводки();
		Пока О.ПолучитьПроводку()=1 Цикл
			Результат.НоваяСтрока();
			
			Результат.СчетДТ=СокрЛП(О.Дебет.Счет.Код);
			Для С=1 По О.Дебет.Счет.КоличествоСубконто() Цикл
				Результат.УстановитьЗначение(Результат.КоличествоСтрок(),"ДтСубконто"+С,О.Дебет.Субконто(С));
			КонецЦикла;
			
			Результат.СчетКТ=СокрЛП(О.Кредит.Счет.Код);
			Для С=1 По О.Кредит.Счет.КоличествоСубконто() Цикл
				Результат.УстановитьЗначение(Результат.КоличествоСтрок(),"КтСубконто"+С,О.Кредит.Субконто(С));
			КонецЦикла;
			
			Для С=1 По Метаданные.РеквизитПроводки() Цикл
				ИдРеквизит=Метаданные.РеквизитПроводки(С).Идентификатор;
				Значение=О.ПолучитьАтрибут(ИдРеквизит);
				Если ТипЗначенияСтр(Значение)="Строка" Тогда
					Значение=СокрЛП(Значение);
				КонецЕсли;
				Результат.УстановитьЗначение(Результат.КоличествоСтрок(),ИдРеквизит,Значение);
			КонецЦикла;
			
			Результат.Сумма=О.Сумма;
			Результат.Количество=О.Количество;
			Результат.ВалСумма=О.ВалСумма;
			Результат.Валюта=О.Валюта;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции