Перем Хранилище Экспорт;
Перем СписокТестов Экспорт;

Процедура Инит(пХранилище, пСписокТестов) Экспорт
	Хранилище=пХранилище;
	СписокТестов=пСписокТестов;
КонецПроцедуры
//--------------------------------------------

Функция НомерТеста(фПоТекущейСтроке=0) Экспорт
	НомерСтроки=?(фПоТекущейСтроке=0, СписокТестов.НомерСтроки, СписокТестов.ТекущаяСтрока());
	Возврат НомерСтроки - Хранилище.ПолучитьКоличествоКаталогов();
КонецФункции
//--------------------------------------------

Функция ЭтоКаталог(фПоТекущейСтроке=0) Экспорт
	Возврат ?(НомерТеста(фПоТекущейСтроке)<=0, 1, 0);
КонецФункции
//--------------------------------------------

Функция RGB(R, G, B) Экспорт
	Возврат R + 256*G + 256*256*B;
КонецФункции    
//--------------------------------------------

Процедура РаскраситьКаталоги() Экспорт
	СписокТестов.ВыбратьСтроки();
	Пока СписокТестов.ПолучитьСтроку()=1 Цикл
		Если ЭтоКаталог()=1 Тогда
			СписокТестов.Цвет = "BRUSH["+RGB(255, 255, 220)+"]";   
			СписокТестов.Пиктограмма = 8;   
			СписокТестов.Номер = "К";
		Иначе
			СписокТестов.Цвет = ""; 
			СписокТестов.Пиктограмма = 12;//7  
			СписокТестов.Номер = НомерТеста();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
//--------------------------------------------

Процедура Обновить() Экспорт
	_СписокТестов = СоздатьОбъект("ТаблицаЗначений");
	Хранилище.ПолучитьСписокТестов(_СписокТестов);
	
	СписокТестов.Очистить();
	_СписокТестов.Выгрузить(СписокТестов); 
   
	СписокТестов.ВставитьКолонку("Цвет", 1);   
	СписокТестов.ВставитьКолонку("Пиктограмма",2,"Число",,,"",4);
	СписокТестов.ВставитьКолонку("Номер", 3,,,, "№", 3);  
	
	СписокТестов.ВыводитьПиктограммы("Пиктограмма",1);
	СписокТестов.Фиксировать(0,1);
	
	СписокТестов.ВидимостьКолонки("Цвет,ИмяФайлаРезультатов", 0);
	РаскраситьКаталоги();
КонецПроцедуры
//--------------------------------------------

Процедура АнализКаталога(лИмяКаталога) Экспорт
	Хранилище.УстановитьТекущийКаталог(лИмяКаталога);
	Обновить();
	// Для того, чтобы отображалось начало таблицы значений, надо сначала перейти на первую строку
	СписокТестов.ТекущаяСтрока(1);
	СписокТестов.ТекущаяКолонка("НазваниеТеста");
КонецПроцедуры
//--------------------------------------------

Процедура ВыбратьСтроку(НомерСтроки) Экспорт
	СписокТестов.ТекущаяСтрока(НомерСтроки);
	СписокТестов.ПолучитьСтрокуПоНомеру(НомерСтроки);
КонецПроцедуры
//--------------------------------------------

Процедура ПерейтиВВыбранныйКаталог(пИмяКаталога) Экспорт
	Если СписокТестов.КоличествоСтрок()=0 Тогда Возврат; КонецЕсли;
	Если ЭтоКаталог()=0 Тогда Возврат; КонецЕсли;
	
	пИмяКаталога = СписокТестов.ИмяФайлаРезультатов;
	АнализКаталога(пИмяКаталога);
КонецПроцедуры
//--------------------------------------------

Процедура ПерейтиККаталогу(пИмяКаталога) Экспорт
	НомерСтрокиКаталога = Хранилище.НомерКаталогаПоИмени(пИмяКаталога);
	ВыбратьСтроку(НомерСтрокиКаталога);
КонецПроцедуры
//--------------------------------------------

Процедура ПерейтиВРодительскийКаталог(пИмяКаталога) Экспорт
	ИмяПрошлогоКаталога=Хранилище.ПолучитьТекущийКаталог();
	пИмяКаталога=Хранилище.ПолучитьРодительскийКаталог();
	АнализКаталога(пИмяКаталога);
	ПерейтиККаталогу(ИмяПрошлогоКаталога);
КонецПроцедуры
//--------------------------------------------

Процедура УдалитьТест() Экспорт
	Хранилище.УдалитьТест(СписокТестов.ТекущаяСтрока());
	Обновить();
КонецПроцедуры
//--------------------------------------------

Процедура СоздатьКаталог() Экспорт
	ИмяНовогоКаталога="";
	ВвестиСтроку(ИмяНовогоКаталога,"Введите имя нового каталога", 60);
	ИмяНовогоКаталога=СокрЛП(ИмяНовогоКаталога);
	Если ИмяНовогоКаталога="" Тогда Возврат; КонецЕсли;
	
	текКаталог=Хранилище.ПолучитьТекущийКаталог();
	ИмяНовогоКаталога = текКаталог+"\"+ИмяНовогоКаталога;
	ФС.СоздатьКаталог(ИмяНовогоКаталога);
	АнализКаталога(текКаталог);
	ПерейтиККаталогу(ИмяНовогоКаталога);
КонецПроцедуры
