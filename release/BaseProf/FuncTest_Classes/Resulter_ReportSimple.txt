// Resulter_ReportSimple

Перем 
		_СписокТаблицДоОткрытияОтчета,
		_Сервис
		;

// Тест // : TheTest		

Функция Этот(Конт) Возврат Конт; КонецФункции
Функция Сам() Возврат Этот(Контекст); КонецФункции

Процедура ЗакрытьТаблицу(лТаблица)
	Если лТаблица <> 0 Тогда
		лТаблица.Показать("", "", -1);
	КонецЕсли;
КонецПроцедуры

Процедура ЗапуститьОтчетПоФормуле(КонтекстОтчета, ФормулаЗапуска)
	ВМ = СоздатьОбъект("ВыполняемыйМодуль");
	ВМ.УстановитьМодуль(ФормулаЗапуска);

	ВМ.НазначитьКонтекст(КонтекстОтчета);
	ВМ.ПрисоединитьТекущийМодуль();
	
	ВМ.КомпилироватьИВыполнитьМодуль();
КонецПроцедуры	// ВыполнитьФормулуЗапускаОтчета

Функция ЗапомнитьВсеУжеОткрытыеТаблицы()
	_Сервис = СоздатьОбъект("Сервис");
	_СписокТаблицДоОткрытияОтчета = _Сервис.СписокТаблиц();
КонецФункции

// TODO если открыто несколько новых таблиц, возможно, нужно дать пользователю выбор  :)
Функция НайтиПервуюТаблицуПоявившуюсяПослеВыполненияОтчета()
	лСписокТаблицПослеОткрытияОтчета = _Сервис.СписокТаблиц();
	
	лНайденнаяТаблица = 0;
	Для к=1 По лСписокТаблицПослеОткрытияОтчета.РазмерСписка() Цикл
		лТаблица = лСписокТаблицПослеОткрытияОтчета.ПолучитьЗначение(к);
		
		Если _СписокТаблицДоОткрытияОтчета.НайтиЗначение(лТаблица) = 0 Тогда
			
			// закрываю остальные открываемые таблицы, кроме первой, чтобы не мешались на экране
			Если лНайденнаяТаблица = 0 Тогда
				лНайденнаяТаблица = лТаблица;
			Иначе
				ЗакрытьТаблицу(лТаблица);
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат лНайденнаяТаблица;
КонецФункции

Функция ЗакрытьОтчетИТаблицу(КонтекстОтчета, Таблица)
	Система = СоздатьОбъект("Система");
	Система.ЗакрытьФорму(КонтекстОтчета);
    
	ЗакрытьТаблицу(Таблица);
КонецФункции

// виртуальная функция для переопределения в наследниках Resulter_ReportMXL
Функция ПолучитьПечатнуюТаблицуИзОтчета(Тест) Экспорт

	КонтекстОтчета = Сам().ОткрытьФормуОтчета(Тест, "");
	Условия = Тест.ПолучитьУсловияКакСписокЗначений();
	Сам().ЗаполнитьФормуНастройкамиТеста(КонтекстОтчета, Условия);
	
	ЗапомнитьВсеУжеОткрытыеТаблицы();
	ЗапуститьОтчетПоФормуле(КонтекстОтчета, Тест.ПолучитьУсловие(Сам().ВернутьНаименованиеУсловия_ФормулаЗапуска()));
	
	Таблица = НайтиПервуюТаблицуПоявившуюсяПослеВыполненияОтчета();
	
	ЗакрытьОтчетИТаблицу(КонтекстОтчета, Таблица);
	
	Возврат Таблица;
КонецФункции
